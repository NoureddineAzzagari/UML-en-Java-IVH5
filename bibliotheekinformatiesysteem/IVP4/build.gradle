apply plugin: "java"
apply plugin: "jacoco"
apply plugin: "eclipse"
apply plugin: "sonar-runner"

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.16' 
    testCompile group: 'junit', name: 'junit', version: '4.+'
}


task listJars << {
    configurations.compile.each { File file -> println file.name }
}


task buildDatabase << {
	println "Connecting to mysql ..."  
	runScript("src/travel.sql", "jdbc:mysql://localhost:3306/test", "root", "password")  
}

def runScript(scriptName, url, user, password) {
	ant.sql(src:scriptName,   
        driver:'com.mysql.jdbc.Driver',   
        print:true,  
        url:url,   
        userid:user,   
        password:password,            
        onerror:'continue'  
    )  
}

jacoco {
    toolVersion = "0.6.2.201302030002"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco"
    }
}

sourceCompatibility = 7		// Geeft aan dat dit project Java 7 of hoger gebruikt.
version = '1.0'				// De versie van dit project. Komt postfix in naam van .jar
// Dit werkt nu nog niet. Dat betekent dat de naam van de .jar IVP4 is.
// rootProject.name = 'IVP4-workedexample-library'
jar {
    manifest {
        attributes 'Implementation-Title': 'Avans AE&I Worked example', 
			'Implementation-Version': version,
			'Main-Class': 'library.main.Main'
    }
}

sourceSets {
    main {
        java { srcDir 'src/java' }
        resources { srcDir 'src/resources' }
    }
    test {
        java { srcDir 'test/java' }
        resources { srcDir 'test/resources' }
    }
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

//
// Deze task opent de jUnit testrapportage in de default browser.
//
task showJunit(type:Exec) {
  workingDir './build/reports/tests'

  //on windows:
  commandLine 'cmd', '/c', 'start index.html'
}

//
// Deze task opent de Jacoco testrapportage in de default browser.
//
task showJacoco(type:Exec) {
  workingDir './build/reports/jacoco'

  //on windows:
  commandLine 'cmd', '/c', 'start index.html'
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://my.server.com"
        property "sonar.jdbc.url", "jdbc:mysql://my.server.com/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "Fred Flintstone"
        property "sonar.jdbc.password", "very clever"
    }
}
